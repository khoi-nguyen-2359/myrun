plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
    id "build-time-tracker"
}

android {
    compileSdkVersion project.compileSdk

    defaultConfig {
        applicationId project.appId
        minSdkVersion project.minSdk
        targetSdkVersion project.targetSdk
        versionCode project.versionCode
        versionName project.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        rc {
            setRoot 'src/release'
        }
    }

    signingConfigs {
        debug {
            storeFile file('./keystores/myrun_debug_key_v2.jks')
            storePassword 'myrundebugkeyv2'
            keyAlias = 'myrun_debug_key_v2'
            keyPassword 'myrundebugkeyv2'
        }
        rc {
            def path = project.localProps['signing.rc.path'] ?: "invalid"
            def password = project.localProps['signing.rc.password']
            def alias = project.localProps['signing.rc.alias']
            storeFile file(path)
            storePassword password
            keyAlias alias
            keyPassword password
        }
    }

    buildTypes {
        release {
            debuggable false

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        rc {
            initWith release
            debuggable true
            signingConfig signingConfigs.rc
        }

        debug {
            applicationIdSuffix ".debug"

            signingConfig signingConfigs.debug
        }
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.composecompiler.get()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
        allWarningsAsErrors = true
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
    }

    lintOptions {
        disable 'NullSafeMutableLiveData' // bypass this bug https://issuetracker.google.com/issues/169249668
    }
}

dependencies {
    implementation project(":feature:core")
    implementation project(":feature:registration")
    implementation project(":feature:profile")
    implementation project(":feature:activity")
    implementation project(":feature:tracking")

    implementation project(':worker')
    implementation(libs.workmanager) // to config work manager in application class

    implementation project(':domain')

    implementation(project(':data:user'))
    implementation(project(':data:tracking'))

    implementation project(':base:di')
    implementation project(':base:firebase')
    implementation platform(libs.firebase.bom)
    implementation(libs.bundles.firebase.data)
    implementation(libs.firebase.perf.ktx)

    implementation(libs.googleplaces)   // to init place sdk in application class

    implementation(libs.kotlinx.coroutines.core)

    implementation(libs.dagger)
    implementation(libs.anvil.annotations)
    kapt(libs.daggercompiler)

    implementation(libs.androidx.core)
    implementation(libs.androidx.appcompat)
    implementation(libs.splashscreen)

    implementation(libs.lifecycle.process)

    implementation(libs.bundles.compose)
    implementation(libs.accompanist.insets)

    implementation(libs.multidex)

    implementation(libs.bundles.crashlytics)

    implementation(libs.timber)

    debugImplementation project(":feature:route")
    debugImplementation project(":feature:configurator")
    if (localProps.getProperty("leakcanary.enabled", "1") == "1") {
        debugImplementation(libs.leakcanary)
    }

    testImplementation(libs.kotlintest)
    testImplementation(libs.mockito.kotlin)
    testImplementation(project(":feature:configurator"))
    testImplementation(project(":feature:route"))
    testImplementation(libs.turbine)
    testImplementation(libs.kotlinx.coroutinestest)
}

buildtimetracker {
    reporters {
        csv {
            output "build/times.csv"
            append true
            header true
        }

        summary {
            ordered false
            threshold 500
            barstyle "unicode"
            shortenTaskNames false
        }

        csvSummary {
            csv "build/times.csv"
        }
    }
}
